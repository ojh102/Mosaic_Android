apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../versioning.gradle'

def rawBuildNumber = System.getenv("BITRISE_BUILD_NUMBER") ?: "0"
def buildNumber = Integer.parseInt(rawBuildNumber)

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.teamnexters.mosaic"
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version

        versionCode buildVersionCode()
        versionName buildVersionName(mosaicVersion, buildNumber)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/debug.jks")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        release {
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
            storeFile file(System.getenv("HOME") + "/keystores/my_keystore.jks")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "BASE_URL", "\"\"")

            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"

            debuggable true
            zipAlignEnabled false
            minifyEnabled false
            multiDexEnabled true
        }

        qa {
            buildConfigField("String", "BASE_URL", "\"\"")

            signingConfig signingConfigs.debug
            applicationIdSuffix ".qa"

            debuggable true
            zipAlignEnabled true
            minifyEnabled true
            multiDexEnabled false
            shrinkResources true
            crunchPngs true

            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles file('../proguard').listFiles()
            println(proguardFiles)
        }

        release {
            buildConfigField("String", "BASE_URL", "\"\"")

            signingConfig null

            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            multiDexEnabled false
            shrinkResources true
            crunchPngs true

            proguardFiles getDefaultProguardFile('proguard-android.txt')
            proguardFiles file('../proguard').listFiles()
            println(proguardFiles)
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = getApkName(mosaicVersion, variant.buildType.name, buildNumber)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlin_version"

    // Support Lib
    implementation "com.android.support:support-v4:$rootProject.support_library_version"
    implementation "com.android.support:appcompat-v7:$rootProject.support_library_version"
    implementation "com.android.support:support-annotations:$rootProject.support_library_version"
    implementation "com.android.support:design:$rootProject.support_library_version"
    implementation "com.android.support:recyclerview-v7:$rootProject.support_library_version"
    implementation "com.android.support:support-emoji:$rootProject.support_library_version"
    implementation "com.android.support:support-emoji-appcompat:$rootProject.support_library_version"
    implementation "com.android.support:support-emoji-bundled:$rootProject.support_library_version"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraint_layout_version"
    kapt "com.android.databinding:compiler:$rootProject.android_gradle_version"

    // Firebase
    implementation "com.google.firebase:firebase-core:$rootProject.firebase_version"
    implementation "com.google.firebase:firebase-messaging:$rootProject.firebase_messaging_version"

    // AAC
    implementation "android.arch.lifecycle:extensions:$rootProject.lifecycle_version"
    implementation "android.arch.paging:runtime:$rootProject.paging_version"
    kapt "android.arch.lifecycle:compiler:$rootProject.lifecycle_version"

    // DI
    implementation "com.google.dagger:dagger:$rootProject.dagger_version"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger_version"
    implementation "com.google.dagger:dagger-android:$rootProject.dagger_version"
    compileOnly 'javax.annotation:jsr250-api:1.0'
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger_version"

    // Retrofit & HTTP
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttp_version"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rx_java_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rx_android_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rootProject.rx_kotlin_version"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rootProject.rx_relay_version"

    // Lottie
    implementation "com.airbnb.android:lottie:$rootProject.lottie_version"

    // Bottom Navigation Ex
    implementation "com.github.ittianyu:BottomNavigationViewEx:$rootProject.bottom_navigation_ex"

    // Debug
    implementation "com.jakewharton.timber:timber:$rootProject.timber_version"
    implementation "com.facebook.stetho:stetho:$rootProject.stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$rootProject.stetho_version"

    // Test
    testImplementation "junit:junit:$rootProject.junit_version"
    androidTestImplementation "com.android.support.test:runner:$rootProject.runner_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espresso_version"
}
